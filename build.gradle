plugins {
    id 'java'
    id 'application'
    id 'org.javamodularity.moduleplugin' version '1.8.15'
    id 'org.openjfx.javafxplugin' version '0.1.0'
    id 'org.beryx.jlink' version '3.1.1'
}

group = 'io.github.uniclog'
version = '1.3.3'

repositories {
    mavenCentral()
}

ext {
    junitVersion = '5.9.2'
    richtextfxVersion = '0.11.4'
    gsonVersion = '2.12.1'
    lombokVersion = '1.18.36'
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
}

application {
    mainModule = 'io.github.uniclog.jsticky'
    mainClass = 'io.github.uniclog.jsticky.App'
}

javafx {
    version = '23.0.2'
    modules = ['javafx.controls', 'javafx.fxml']
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(23)
    }
}

dependencies {
    // implementation('com.dlsc.formsfx:formsfx-core:11.6.0') {
    //     exclude(group: 'org.openjfx')
    // }
    implementation 'org.openjfx:javafx-controls:17.0.14'
    implementation 'org.openjfx:javafx-fxml:21.0.1'
    implementation 'org.openjfx:javafx-base:21.0.1'
    implementation 'org.openjfx:javafx-graphics:21.0.1'
    implementation 'org.fxmisc.richtext:richtextfx:0.11.4'

    implementation("com.google.code.gson:gson:${gsonVersion}")
    annotationProcessor("org.projectlombok:lombok:${lombokVersion}")
    implementation("org.projectlombok:lombok:${lombokVersion}")

    testImplementation("org.junit.jupiter:junit-jupiter-api:${junitVersion}")
    testRuntimeOnly("org.junit.jupiter:junit-jupiter-engine:${junitVersion}")
}

test {
    useJUnitPlatform()
}

jlink {
    imageZip = layout.buildDirectory.file("distributions/jSticky-${javafx.platform.classifier}.zip").get()
    options = ['--strip-debug', '--compress', '2', '--no-header-files', '--no-man-pages']
    launcher {
        name = 'jSticky'
    }
    addExtraDependencies('javafx')
    jpackage {
        appVersion = "${version}"
        vendor = 'io.github.uniclog'
        icon = "src/main/resources/io/github/uniclog/jsticky/img/icon.ico"
        imageOptions += [
                "--vendor", "Uniclog Software",
                '--app-version', version
        ]
        installerType = 'msi'
        installerOptions = [
                '--win-dir-chooser', '--win-shortcut'
        ]
    }
}

jlinkZip {
    group = 'distribution'
}
