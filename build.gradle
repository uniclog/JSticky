plugins {
    id 'java'
    id 'application'
    id 'org.javamodularity.moduleplugin' version '1.8.12'
    id 'org.openjfx.javafxplugin' version '0.0.13'
    id 'org.beryx.jlink' version '3.0.0'
}

group 'io.github.uniclog'
version '1.2'

repositories {
    mavenCentral()
}

ext {
    junitVersion = '5.9.2'
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
}

application {
    mainModule = 'io.github.uniclog.jsticky'
    mainClass = 'io.github.uniclog.jsticky.App'
}

javafx {
    version = '21'
    modules = ['javafx.controls', 'javafx.fxml']
}

dependencies {
    implementation('com.dlsc.formsfx:formsfx-core:11.6.0') {
        exclude(group: 'org.openjfx')
    }
    implementation 'com.google.code.gson:gson:2.10.1'
    annotationProcessor 'org.projectlombok:lombok:1.18.26'
    implementation 'org.projectlombok:lombok:1.18.26'

    testImplementation("org.junit.jupiter:junit-jupiter-api:${junitVersion}")
    testRuntimeOnly("org.junit.jupiter:junit-jupiter-engine:${junitVersion}")
}

test {
    useJUnitPlatform()
}

jlink {
    imageZip = project.file("${buildDir}/distributions/jSticky-${javafx.platform.classifier}.zip") as RegularFile
    options = ['--strip-debug', '--compress', '2', '--no-header-files', '--no-man-pages']
    launcher {
        name = 'jSticky'
    }
    forceMerge 'kotlin'
    jpackage {
        appVersion = '1.2'
        vendor = 'io.github.uniclog'
        icon = "src/main/resources/io/github/uniclog/jsticky/img/icon.ico"
        imageOptions = [
                "--vendor", "Uniclog Software",
                '--app-version', version
        ] as List<String>
        installerType = 'msi'
        installerOptions = [
                '--win-dir-chooser', '--win-shortcut'
        ]
    }
}

jlinkZip {
    group = 'distribution'
}
